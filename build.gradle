
subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java-library"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

ext {
    projectGroup = "ashley"
    annotationsVersion = "16.0.2"
    gdxVersion = "1.9.10"
    jUnitVersion = "4.12"
    mockitoVersion = "1.10.19"
}

/** needed to disable Java 8 doclint which throws errors **/
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

project(":ashley") {
    apply from : '../publish.gradle'

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        compileOnly "org.jetbrains:annotations:$annotationsVersion"
        testImplementation "junit:junit:$jUnitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
    }
}

project(":tests") {
    dependencies {
        implementation project(":ashley")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":benchmarks") {
    dependencies {
        implementation project(":ashley")
        implementation "junit:junit:$jUnitVersion"
        implementation fileTree(dir: new File(projectDir, "libs"), include: "*.jar")
    }
}

