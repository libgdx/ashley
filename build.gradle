
subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

ext {
    projectGroup = "ashley"
    gdxVersion = "1.9.9"
    jUnitVersion = "4.12"
    mockitoVersion = "1.10.19"
}

/** needed to disable Java 8 doclint which throws errors **/
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

project(":ashley") {
    apply from : '../publish.gradle'

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        testCompile "junit:junit:$jUnitVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
    }
}

project(":tests") {
    dependencies {
        compile project(":ashley")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project(":benchmarks") {
    dependencies {
        compile project(":ashley")
        compile "junit:junit:$jUnitVersion"
        compile fileTree(dir: new File(projectDir, "libs"), include: "*.jar")
    }
}

